buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath lib.plugin.bintray
        classpath lib.plugin.dcendents
        classpath lib.plugin.dokka
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'com.github.ben-manes.versions' version '0.21.0'
}

ext {
    bintrayRepo = 'Android'
    bintrayName = 'Domain'

    publishedGroupId = 'com.artear.domain'
    libraryName = 'Domain'
    artifact = 'domain'

    libraryDescription = 'A useful pack of classes for represent a use case, that works ' +
            'like a bridge between repositories and view models.'

    siteUrl = 'https://github.com/Artear/app_lib_domain_android'
    gitUrl = 'https://github.com/Artear/app_lib_domain_android.git'

    developerId = 'artear'
    developerName = 'Artear Moviles'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

static int getVersionCode(branch) {
    def versionCodeCommand = "git rev-list --count " + branch
    return versionCodeCommand.execute().text.trim().toInteger()
}

static def getBranchName() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

android {
    compileSdkVersion sdkVersion
    buildToolsVersion buildToolsVersion

    def branch = getBranchName()
    def vCode = 0 + getVersionCode(branch)

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion sdkVersion
        versionCode vCode
        versionName libraryVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Test
    testImplementation lib.junit.library
    androidTestImplementation lib.androidx.testCore
    androidTestImplementation lib.androidx.extJunit
    androidTestImplementation lib.androidx.testRunner
    androidTestImplementation lib.espresso.core

    //Kotlin
    implementation lib.kotlin.standardLib

    //Tools
    api lib.artear.tools

    //Coroutines
    implementation lib.coroutines.core
    implementation lib.coroutines.android

    //ReactiveX
    implementation lib.reactivex.rxjava
    implementation lib.reactivex.rxandroid
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

group = publishedGroupId
version = libraryVersion

apply from: 'publication.gradle'
apply from: 'bintray.gradle'
